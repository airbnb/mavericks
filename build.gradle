import com.airbnb.mvrx.AndroidMetaModulePlugin

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.gradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${Versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:${Versions.hilt}"

        // Upload with:
        // ./gradlew clean assemble uploadArchives --no-daemon --no-parallel
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.14.2"
        // Dokka is needed on classpath for vanniktech publish plugin
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.30"
    }

    configurations.all {
        resolutionStrategy {
            // Use Proguard 6.2.0
            force "net.sf.proguard:proguard-gradle:6.2.0"
        }
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.16.0-RC2")
}

apply plugin: 'io.gitlab.arturbosch.detekt'

plugins.apply(AndroidMetaModulePlugin.class)

allprojects {
    version = VERSION_NAME
    group = GROUP

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply from: "$rootDir/detekt.gradle"
}
